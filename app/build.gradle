apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'com.google.firebase.crashlytics'

android {

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {

        def versionMajor = 1
        def versionMinor = 3
        def versionPatch = 1

        applicationId "com.github.odaridavid.designpatterns"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "$versionMajor.$versionMinor.$versionPatch"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    crashlyticsEnabled: true,
                    appIcon           : "@mipmap/ic_launcher",
                    appIconRound      : "@mipmap/ic_launcher_round"
            ]
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [
                    crashlyticsEnabled: false,
                    appIcon           : "@mipmap/ic_debug_launcher",
                    appIconRound      : "@mipmap/ic_debug_launcher_round"
            ]
        }
    }
    viewBinding.enabled = true
    kotlinOptions.jvmTarget = 1.8

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {

    def about_libraries_version = "8.9.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.21"
    implementation 'androidx.appcompat:appcompat:1.4.0-alpha03'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    // Playstore
    implementation 'com.google.android.play:core:1.10.2'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    //Material Design
    implementation "com.google.android.material:material:1.5.0-alpha01"

    //Libraries used info
    implementation "com.mikepenz:aboutlibraries-core:$about_libraries_version"
    implementation "com.mikepenz:aboutlibraries:$about_libraries_version"

    //Animations
    implementation 'jp.wasabeef:recyclerview-animators:3.0.0'

    //Analytics
    implementation 'com.google.firebase:firebase-analytics:19.0.0'

    //Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:18.2.0'

    //Preference
    implementation 'androidx.preference:preference-ktx:1.1.1'

    //Markdown Support
    implementation 'us.feras.mdv:markdownview:1.1.0'

    //Unit testing
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
}
apply plugin: 'com.google.gms.google-services'
